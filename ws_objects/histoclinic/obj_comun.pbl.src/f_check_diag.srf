$PBExportHeader$f_check_diag.srf
global type f_check_diag from function_object
end type

forward prototypes
global function st_return_diags f_check_diag ()
global function st_return_diags f_check_diag (string diagnos, string sexo_pac, double edad, ref string este, string pante)
end prototypes

global function st_return_diags f_check_diag ();

//
st_return_diags st

return st
end function

global function st_return_diags f_check_diag (string diagnos, string sexo_pac, double edad, ref string este, string pante);// la edad debe venir en dias
// diagnos es el codigo que digita el medico
// este es codgeral
long edad_ini,edad_fin,versions
datetime ahora
string sexo,retorna,version,temp
st_return_diags st

ahora=datetime(today())
setnull(sexo)
if pante='0' then 
	
	if este='' or isnull(este) then
		SELECT Diags.c_version,Count(Diags.CodGeral) into :version,:versions
		FROM Diags INNER JOIN diags_version ON Diags.c_version = diags_version.c_version
		WHERE (((Diags.cod_rips)=:diagnos) AND ((diags_version.val_hasta)>=:ahora) and  Diags.estado='1')
		GROUP BY Diags.c_version;
		
		if sqlca.sqlcode=-1 then
			messagebox("Error leyendo tabla diags1",sqlca.sqlerrtext)
			st.descrip_diag=''
			return st
		end if
		if versions=0 then
			Messagebox("Atención Validación","Este Diagnóstico no existe o su versión ya no es válida ")
			st.descrip_diag=''
			return st
		end if
		if versions> 1 then
			Messagebox("Atención","Hay más de una versión válida para este diagnóstico, revise las versiones de diagnósticos")
			st.descrip_diag=''
			return st
		end if
		if g_motor='postgres' then
			SELECT Diags.c_version, Diags.desdiag, capdiags.antecedente  into :version , :retorna,:st.antecedente
			FROM capdiags INNER JOIN diags ON (diags.c_version = capdiags.c_version) AND (capdiags.codcap = diags.ccap)
			WHERE Ccap || coddiag=:diagnos AND  capdiags.c_version=:version and  Diags.estado='1';
		else
			SELECT Diags.c_version, Diags.desdiag, capdiags.antecedente  into :version , :retorna,:st.antecedente
			FROM capdiags INNER JOIN diags ON (diags.c_version = capdiags.c_version) AND (capdiags.codcap = diags.ccap)
			WHERE Ccap + coddiag=:diagnos AND  capdiags.c_version=:version and  Diags.estado='1';
		end if
		if sqlca.sqlcode=-1 then
			messagebox("Error leyendo tabla diags2",sqlca.sqlerrtext)
			st.descrip_diag=''
			return st
		end if
		string cap,diag
		SELECT Diags.desdiag,Diags.c_version,Diags.ccap,Diags.coddiag, capdiags.antecedente into :retorna,:version,:cap,:diag,:st.antecedente
		FROM (diags_version INNER JOIN capdiags ON diags_version.c_version = capdiags.c_version) INNER JOIN Diags ON (capdiags.c_version = Diags.c_version) AND (capdiags.codcap = Diags.ccap)
		WHERE Diags.cod_rips=:diagnos AND diags_version.val_hasta>=:ahora and  Diags.estado='1';
		if sqlca.sqlcode=-1 then
			messagebox("Error leyendo tabla diags3",sqlca.sqlerrtext)
			st.descrip_diag=''
			return st
		end if
		este=cap+diag+version
	else
		SELECT diags.desdiag, capdiags.antecedente into :retorna,:st.antecedente
		FROM capdiags INNER JOIN diags ON (diags.c_version = capdiags.c_version) AND (capdiags.codcap = diags.ccap)
		WHERE codgeral=:este and estado='1';
		if sqlca.sqlcode=-1 then
			messagebox("Error leyendo tabla diags4",sqlca.sqlerrtext)
			st.descrip_diag=''
			return st
		end if
	end if
	SELECT DiagEdad.EdadIni, DiagEdad.EdadFin, DiagEdad.IndSexo
	INTO :edad_ini, :edad_fin, :sexo
	FROM DiagEdad
	WHERE DiagEdad.CodGeral=:este;
	if sqlca.sqlcode=-1 then
		messagebox("Error leyendo tabla diags5",sqlca.sqlerrtext)
		st.descrip_diag=''
		return st
	end if
	if isnull(sexo) then
		messagebox("Atención","No se puede validar este dianóstico, no está en la tabla DIAGEDAD")
		st.descrip_diag=''
		return st
	end if
	choose case sexo
		case "1" //HOMBRE
			if sexo_pac="F" then
				Messagebox("Atención","Este diagnóstico no es válido para el sexo del paciente")
				st.descrip_diag=''
				return st
			end if
		case "2" //MUJER
			if sexo_pac="M" then
				Messagebox("Atención","Este diagnóstico no es válido para el sexo del paciente")
				st.descrip_diag=''
				return st
			end if
	end choose
	if edad<edad_ini or edad>edad_fin then
		if Messagebox("Atención","Este diagnóstico no es válido para la edad del paciente. Desea utilizarlo aún así?",question!,yesno!,2)=1 then
			st.descrip_diag=retorna
			return st
		else
			st.descrip_diag=''
			return st
		end if
	end if
	
	st.codgeral=este
	st.descrip_diag=retorna
else
end if
return st
end function

