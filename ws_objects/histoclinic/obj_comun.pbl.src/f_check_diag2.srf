$PBExportHeader$f_check_diag2.srf
global type f_check_diag2 from function_object
end type

forward prototypes
global function st_return_diags f_check_diag2 ()
global function st_return_diags f_check_diag2 (string ps_diagnos, string ps_sexo_pac, double pdb_edad, string ps_servicio, string ps_dxrel)
end prototypes

global function st_return_diags f_check_diag2 ();st_return_diags st

return st
end function

global function st_return_diags f_check_diag2 (string ps_diagnos, string ps_sexo_pac, double pdb_edad, string ps_servicio, string ps_dxrel);//devuelve Descripcion del diag si todo OK o "" si hubo error
// la edad debe venir en dias
// diagnos es el codigo que digita el medico
// p_codgeral es codgeral
long ld_edad_ini,ld_edad_fin,ldb_ctos
datetime ldt_ahora
st_return_diags st
string ls_sexo,ls_version,ls_dxrel
ldt_ahora=datetime(today())
setnull(ls_sexo)

SELECT Count(Diags.CodGeral) into :ldb_ctos
FROM Diags INNER JOIN diags_version ON Diags.c_version = diags_version.c_version
WHERE (((Diags.cod_rips)=:ps_diagnos) AND ((diags_version.val_hasta)>=:ldt_ahora) AND ((Diags.estado)='1'));

if sqlca.sqlcode=-1 then
	messagebox("Error leyendo tabla diags f_check_diag2 Linea 17",sqlca.sqlerrtext)
	st.descrip_diag=''
	return st
end if
if ldb_ctos=0 then
	Messagebox("Atención","p_codgeral Diagnóstico no existe o su versión ya no es válida")
	st.descrip_diag=''
	return st
end if
if ldb_ctos> 1 then
	Messagebox("Atención","Hay más de una versión válida para p_codgeral diagnóstico, revise las versiones de diagnósticos")
	st.descrip_diag=''
	return st
end if

if g_motor='postgres' then
	SELECT  
		diags.c_version, diags.desdiag, diags.cod_vigila, diags_vigila.codplantilla, diags.guia, capdiags.antecedente,diags.morbilidad
	INTO 
		:ls_version , :st.descrip_diag,:st.vigila,:st.plantilla,:st.guia,:st.antecedente,:ls_dxrel
	FROM 
		((diags LEFT JOIN diags_vigila ON diags.cod_vigila = diags_vigila.cod_vigila) 
		INNER JOIN diags_version ON diags.c_version = diags_version.c_version) 
		INNER JOIN capdiags ON (diags.ccap = capdiags.codcap) AND (diags.c_version = capdiags.c_version)
	WHERE 
		(((Ccap||coddiag)=:ps_diagnos) AND ((diags_version.val_hasta)>=:ldt_ahora) AND ((diags.estado)='1'));
else
	SELECT  
		diags.c_version, diags.desdiag, diags.cod_vigila, diags_vigila.codplantilla, diags.guia, capdiags.antecedente,diags.morbilidad
	INTO 
		:ls_version , :st.descrip_diag,:st.vigila,:st.plantilla,:st.guia,:st.antecedente,:ls_dxrel
	FROM 
		((diags LEFT JOIN diags_vigila ON diags.cod_vigila = diags_vigila.cod_vigila) 
		INNER JOIN diags_version ON diags.c_version = diags_version.c_version) 
		INNER JOIN capdiags ON (diags.ccap = capdiags.codcap) AND (diags.c_version = capdiags.c_version)
	WHERE
			(((Ccap+coddiag)=:ps_diagnos) AND ((diags_version.val_hasta)>=:ldt_ahora) AND ((diags.estado)='1'));
end if
if sqlca.sqlcode=-1 then
	messagebox("Error leyendo tabla diags f_check_diag2 Linea 52",sqlca.sqlerrtext)
	st.descrip_diag=''
	return st
end if

string ls_cap,ls_diag,ls_serv
SELECT 
	Diags.desdiag,Diags.c_version,Diags.ccap,Diags.coddiag,diags.tservicio ,diags.morbilidad
INTO
	:st.descrip_diag,:ls_version,:ls_cap,:ls_diag,:ls_serv,:ls_dxrel
FROM 
	Diags INNER JOIN diags_version ON Diags.c_version = diags_version.c_version
WHERE 
	(((Diags.cod_rips)=:ps_diagnos) AND ((diags_version.val_hasta)>=:ldt_ahora) AND ((Diags.estado)='1'));
	
if sqlca.sqlcode=-1 then
	messagebox("Error leyendo tabla diags f_check_diag2 Linea 61",sqlca.sqlerrtext)
	st.descrip_diag=''
	return st
end if
st.codgeral=ls_cap+ls_diag+ls_version

if ps_dxrel='1' and ls_dxrel='0' then
	Messagebox("Atención","("+ps_diagnos+") diagnóstico no es válido Dx Relacionado")
	st.descrip_diag=''
	return st
end if		
	
if ps_servicio='0' and ls_serv='1' then
	Messagebox("Atención","("+ps_diagnos+") diagnóstico no es válido para procedimientos")
	st.descrip_diag=''
	return st
end if

SELECT 
	DiagEdad.EdadIni, DiagEdad.EdadFin, DiagEdad.IndSexo
INTO 
	:ld_edad_ini, :ld_edad_fin, :ls_sexo
FROM 
	DiagEdad
WHERE 
	DiagEdad.CodGeral=:st.codgeral;
if sqlca.sqlcode=-1 then
	messagebox("Error leyendo tabla diags  f_check_diag2 Linea 87",sqlca.sqlerrtext)
	st.descrip_diag=''
	return st
end if
if isnull(ls_sexo) then
	messagebox("Atención","No se puede validar "+"("+ps_diagnos+") dianóstico, no está en la tabla DIAGEDAD")
	st.descrip_diag=''
	return st
end if

choose case ls_sexo
	case "1" //HOMBRE
		if ps_sexo_pac="F" then
			Messagebox("Atención","("+ps_diagnos+") diagnóstico no es válido para el sexo del paciente")
			st.descrip_diag=''
			return st
		end if
	case "2" //MUJER
		if ps_sexo_pac="M" then
			Messagebox("Atención","("+ps_diagnos+") diagnóstico no es válido para el sexo del paciente")
			st.descrip_diag=''
			return st
		end if
end choose
if pdb_edad<ld_edad_ini or pdb_edad>ld_edad_fin then
	if Messagebox("Atención","("+ps_diagnos+") diagnóstico no es válido para la edad del paciente. Desea utilizarlo aún así?",question!,yesno!,2)=1 then
		return st
	else
		st.descrip_diag=''
		return st
	end if
end if
return st
end function

