$PBExportHeader$gf_revisa_migracion.srf
global type gf_revisa_migracion from function_object
end type

forward prototypes
global function integer gf_revisa_migracion (string as_clug, long al_contador)
end prototypes

global function integer gf_revisa_migracion (string as_clug, long al_contador);return 1

uo_datastore lds_datos
lds_datos= create uo_datastore

lds_datos.dataobject='dw_xa_migrar_hc_individual'
lds_datos.settransobject(sqlca)

if lds_datos.retrieve(as_clug,al_contador)<0 then return -1

if lds_datos.rowcount()=0 then return 0

long j , ll_nregistro , ll_item ,ll_leyo
integer li_result , li_arch

string ls_texto
string ls_ruta,ls_filename
blob lbl_texto


oleobject lo_ole

lo_ole=create oleobject

li_result=lo_ole.ConnectToNewObject("word.application.8")
IF  li_result<> 0 THEN
	messagebox("Atención","No se pudo conectar al OLE de Word!!")
	return -1
end if

for j=1 to lds_datos.rowcount()
	
	ls_texto=lds_datos.getitemstring(j,'long_texto')
	ll_nregistro=lds_datos.getitemnumber(j,'nregistro')
	ll_item=lds_datos.getitemnumber(j,'item')
	
	if pos(ls_texto,'{\rtf',1)=0 then //no es necesario migrar pero debe actualizar el campo migrado
	
		if lds_datos.getitemstring(j,'migrado')<>'1' then
			
			update hclin_reg_valor set migrado='1' 
			where contador=:al_contador and clugar =:as_clug and nregistro =:ll_nregistro and item=:ll_item;
		
			if sqlca.sqlcode<0 then
				messagebox("Error SQL","Error actualizando hclin_reg_valor"+sqlca.sqlerrtext)
				return -1
			end if
			
		end if
		
	else//MIGRAR !!!!!
	
		ls_filename=as_clug+'-'+string(al_contador)+'-'+string(ll_nregistro)+'-'+string(ll_item)
	
	   ls_ruta='c:\windows\temp\'
	
		li_arch=fileopen(ls_ruta+ls_filename+'.rtf',textmode!,Write!,Shared!,replace!,EncodingANSI!)
		ll_leyo=fileWriteEx(li_arch,ls_texto)
		fileclose(li_arch)
		
		if ll_leyo=0 then
			
			messagebox("Error migrando","No se pudo escribir en el archivo: "+ls_ruta+ls_filename+'.rtf')
			
			update hclin_reg_valor set migrado='E' where contador=:al_contador and clugar =:as_clug and nregistro =:ll_nregistro and item=:ll_item;
			
			return -1
			
		end if
		
		try 
			lo_ole.documents.open(ls_ruta+ls_filename+'.rtf')
			
			//Documentación en 
			//https://learn.microsoft.com/en-us/office/vba/api/word.saveas2
			lo_ole.ActiveDocument.SaveAs2(ls_ruta+ls_filename+'.txt',5,false,"",false,"",true,false,true,false,false,65001,true) //65001 es UTF8!
			lo_ole.ActiveDocument.Close()
			
		catch ( RuntimeError ex )
			
			update hclin_reg_valor set migrado='E' where contador=:al_contador and clugar =:as_clug and nregistro =:ll_nregistro and item=:ll_item;
			
			messagebox("Error migrando",ex.getmessage())
			
			return -1
			
		end try
		
		
		li_arch=fileopen(ls_ruta+ls_filename+'.txt',textmode!,Read!,Shared!,replace!,EncodingUTF8!)
		
		ls_texto=""
		
		ll_leyo= filereadex(li_arch,ls_texto)
		fileclose(li_arch)
		
		if ll_leyo<=0 then
						
			update hclin_reg_valor set migrado='E' where contador=:al_contador and clugar =:as_clug and nregistro =:ll_nregistro and item=:ll_item;
			
			messagebox("error migrando", "No se pudo leer desde el archivo: "+ ls_ruta+ls_filename+'.txt')
			
			return -1
			
		else
			
			lbl_texto=blob(ls_texto)
			
			updateblob hclin_reg_valor set long_texto=:lbl_texto 
			where contador=:al_contador and clugar =:as_clug and nregistro =:ll_nregistro and item=:ll_item;
			
			if sqlca.sqlcode<0 then
				
				messagebox("Error en SQL migrando",sqlca.sqlerrtext)
				
				update hclin_reg_valor set migrado='E' where contador=:al_contador and clugar =:as_clug and nregistro =:ll_nregistro and item=:ll_item;
				
				return -1
				
			else
				
				update hclin_reg_valor set migrado='1' where contador=:al_contador and clugar =:as_clug and nregistro =:ll_nregistro and item=:ll_item;
				
				filedelete(ls_ruta+ls_filename+'.txt')
				filedelete(ls_ruta+ls_filename+'.rtf')
				//si todo queda bien, se borran los archivos para no gastar espacio en disco
				//aquí solo se entra cuando la migración del registro queda bien
				
			end if
			
		end if
		
	end if
	
next

lo_ole.Quit()//cierra la instancia de WORD que se abre

lo_ole.DisconnectObject()
destroy lo_ole
destroy lds_datos

return 1
end function

